#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

#define MAX_LOD_LEVEL	1
#define BATCH_SIZE		4

//the structure of instance
struct vertex_instance_data 
{
	vec4	pos;
};

// w_draw_indexed_indirect_command's layout
struct draw_indexed_indirect_command 
{
	uint	index_count;
	uint	instance_count;
	uint	first_index;
	uint	vertex_offset;
	uint	first_instance;
};

//Level Of Detail layout
struct LOD
{
	uint	first_index;
	uint	index_count;
	float	distance;
	float	padding;
};

// input instances buffer
layout (binding = 0, std140) buffer INSTANCES 
{
   vertex_instance_data instances[];
};

// input indirect draws buffer
layout (binding = 1, std430) writeonly buffer INDIRECT_DRAWS
{
	draw_indexed_indirect_command indirect_draws[];
};

// input uniform
layout (binding = 2) uniform UBO_IN
{
	vec4	camera_pos;
	vec4	is_visible;
} i_ubo;

// output compute shader uniform contains status
layout (binding = 3) buffer UBO_OUT
{
	uint draw_count;
	uint lod_levels [MAX_LOD_LEVEL + 1];
	uint padding;
} o_ubo;

layout (binding = 4) readonly buffer LODs
{
	LOD lods[];
};

layout (local_size_x = BATCH_SIZE) in;
void main()
{
	uint _idx = gl_GlobalInvocationID.x + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;
	
	//clear stats on first invocation
	if (_idx == 0)
	{
		atomicExchange(o_ubo.draw_count, 0);
		atomicExchange(o_ubo.lod_levels[0], 0);
		atomicExchange(o_ubo.lod_levels[1], 0);
	}

	if (i_ubo.is_visible[_idx] != 0)
	{
		indirect_draws[_idx].instance_count = 1;
		
		// Increase number of indirect draw counts
		atomicAdd(o_ubo.draw_count, 1);
		
		// Select appropriate LOD level based on distance to camera
		uint _lod_level = MAX_LOD_LEVEL;
		for (uint i = 0; i < MAX_LOD_LEVEL; i++)
		{
			if (distance(instances[_idx].pos.xyz, i_ubo.camera_pos.xyz) <= lods[i].distance) 
			{
				_lod_level = i;
				break;
			}
		}
		indirect_draws[_idx].first_index = lods[_lod_level].first_index;
		indirect_draws[_idx].index_count = lods[_lod_level].index_count;
		
		// Update stats
		atomicAdd(o_ubo.lod_levels[_lod_level], 1);
	}
	else
	{
		indirect_draws[_idx].instance_count = 0;
	}
}